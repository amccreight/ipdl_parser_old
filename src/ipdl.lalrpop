// This Source Code Form is subject to the terms of the Mozilla Public
// License, v. 2.0. If a copy of the MPL was not distributed with this
// file, You can obtain one at http://mozilla.org/MPL/2.0/.

use ast::{Compress, CxxTypeKind, Direction, FileType, Identifier,
          Location, MessageDecl, MessageModifier, Namespace, Nesting,
          Param, Priority, Protocol, QualifiedId, SendSemantics,
          StructField, TranslationUnit, TypeSpec, UsingStmt};

use parser::{TopLevelDecl, ParserState, PreambleStmt};

grammar(parser_state: &ParserState);

//-----------------------------------------------------------------------------

STRING: String = <s:r#""[^"\n]*""#> => String::from(s);

Identifier: Identifier = {
    <start:@L> <id:r"[a-zA-Z_][a-zA-Z0-9_]*"> => {
        let start_loc = parser_state.resolve_location(start);
        Identifier::new(String::from(id), start_loc)
    }
};

//-----------------------------------------------------------------------------

pub TranslationUnit: TranslationUnit = {
    <ps:Preamble> <ns:NamespaceThing+> => {
        let mut cxx_includes = Vec::new();
        let mut includes = Vec::new();
        let mut using = Vec::new();
        for p in ps {
            match p {
                PreambleStmt::CxxInclude(f) => cxx_includes.push(f),
                PreambleStmt::Include(f) => includes.push(f),
                PreambleStmt::Using(u) => using.push(u),
            }
        }

        let mut protocol = None;
        let mut structs = Vec::new();
        let mut unions = Vec::new();
        let mut last_is_struct = false;

        for nts in ns {
            for (ns, d) in nts {
                match d {
                    TopLevelDecl::Struct(s) => {
                        structs.push((ns, s));
                        last_is_struct = true;
                    },
                    TopLevelDecl::Union(u) => {
                        unions.push((ns, u));
                        last_is_struct = false;
                    },
                    TopLevelDecl::Protocol(p) => {
                        if protocol.is_some() {
                            parser_state.add_error(&ns.name.loc,
                                                   "Only one protocol definition per file.");
                        } else {
                            protocol = Some((ns, p));
                        }
                    },
                }
            }
        }

        if protocol.is_none() && parser_state.file_type == FileType::Protocol {
            parser_state.add_error(&Location { file_name: parser_state.file_name.clone(), lineno: 0, colno: 0 },
                                   "Protocol file must contain a protocol.");
        }

        // The "canonical" namespace of the translation unit, ie what
        // it's considered to be in for the purposes of C++:
        // |#include "foo/bar/TU.h"|
        let namespace = match &protocol {
            &Some(ref p) => p.0.clone(),
            &None =>
                // There's not really a canonical "thing" in headers. So
                // somewhat arbitrarily use the namespace of the last
                // interesting thing that was declared.
                if last_is_struct {
                    structs.last().unwrap().0.clone()
                } else {
                    unions.last().unwrap().0.clone()
                }
        };

        TranslationUnit {
            namespace: namespace,
            file_type: parser_state.file_type.clone(),
            file_name: parser_state.file_name.clone(),
            cxx_includes: cxx_includes,
            includes: includes,
            using: using,
            structs: structs,
            unions: unions,
            protocol: protocol
        }
    }
};


//--------------------
// Preamble
Preamble: Vec<PreambleStmt> = {
    (PreambleStmt ";")* => {
        let mut v = Vec::new();
        for (p, _) in <> {
            v.push(p);
        }
        v
    }
};

PreambleStmt = { CxxIncludeStmt, IncludeStmt, UsingStmt };

CxxIncludeStmt: PreambleStmt = {
  "include" <file:STRING> => PreambleStmt::CxxInclude(file),
};

IncludeStmt: PreambleStmt = {
    "include" <p:"protocol"?> <id:Identifier> => {
        let mut file = id.id;
        file.push_str(".ipdl");
        if !p.is_some() {
            file.push_str("h");
        }

        PreambleStmt::Include(parser_state.resolve_include_path(&id.loc, &file))
    }
};

UsingStmt: PreambleStmt = {
    "using" <rc:"refcounted"?> <mo:"moveonly"?> <k:CxxTypeKind?> <t:CxxType> "from" <h:STRING> =>
        PreambleStmt::Using(UsingStmt { cxx_type: t,
                                        header: h,
                                        kind: k,
                                        refcounted: rc.is_some(),
                                        moveonly: mo.is_some()}),
};

CxxTypeKind: CxxTypeKind = {
    "class" => CxxTypeKind::Class,
    "struct" => CxxTypeKind::Struct
};


//--------------------
// Namespaced stuff

NamespaceThing: Vec<(Namespace, TopLevelDecl)> = {
    "namespace" <name:Identifier> "{" <many_things:NamespaceThing+> "}" => {
        let mut things = Vec::new();
        for old_things in many_things {
            for (mut ns, node) in old_things {
                ns.add_outer_namespace(&name.id);
                things.push((ns, node))
            }
        }
        things
    },
    <s:StructDecl> => vec![(s.0, TopLevelDecl::Struct(s.1))],
    <u:UnionDecl> => vec![(u.0, TopLevelDecl::Union(u.1))],
    <p:ProtocolDefn> => vec![(p.0, TopLevelDecl::Protocol(p.1))],
};

StructDecl: (Namespace, Vec<StructField>) = {
    "struct" <name:Identifier> "{" <raw_fields: (StructField ";")* > "}" ";" => {
        let mut fields = Vec::new();
        for (f, _) in raw_fields {
            fields.push(f);
        }
        (Namespace::new(name), fields)
    }
};

StructField: StructField = {
    <t:Type> <field_name:Identifier> => StructField::new(t, field_name)
};

UnionDecl: (Namespace, Vec<TypeSpec>) = {
    "union" <name:Identifier> "{" <raw_components: (Type ";")+ > "}" ";" => {
        let mut components = Vec::new();
        for (c, _) in raw_components {
            components.push(c);
        }
        (Namespace::new(name), components)
    }
};

ProtocolDefn: (Namespace, Protocol) = {
    <q:ProtocolSendSemanticsQual?> "protocol" <name:Identifier> "{"
        <managers:ManagersStmtOpt> <manages:ManagesStmt*> <decls:MessageDeclThing*> "}" ";" =>
    {

        if let FileType::Header = parser_state.file_type {
            parser_state.add_error(&name.loc,
                                   "Can't define a protocol in a header. Do it in a protocol spec instead.");
        }

        let (nested, send_semantics) = q.unwrap_or((Nesting::None, SendSemantics::Async));
        (Namespace::new(name), Protocol::new(send_semantics, nested, managers, manages, decls))
    },
};


//--------------------
// manager/manages stmts

ManagersStmtOpt: Vec<Identifier> = {
    "manager" <l:ManagerList> ";" => l,
    => Vec::new(),
};

ManagerList: Vec<Identifier> = {
    <name:Identifier> => vec![name],
    <l:ManagerList> "or" <name:Identifier> => {
        let mut l = l;
        l.push(name);
        l
    }
};

ManagesStmt: Identifier = {
    "manages" <name:Identifier> ";" => name,
};


//--------------------
// Message decls

MessageDeclThing : MessageDecl = {
    MessageDirectionLabel ":" <m:MessageDecl> ";" => m,
    <m:MessageDecl> ";" => m,
};

MessageDirectionLabel : () = {
    "parent" => parser_state.direction.set(Some(Direction::ToParent)),
    "child" => parser_state.direction.set(Some(Direction::ToChild)),
    "both" => parser_state.direction.set(Some(Direction::ToParentOrChild)),
};

MessageDecl: MessageDecl = {
    <qual:SendSemanticsQual> <msg:MessageBody> => {
        let mut msg = msg;
        msg.nested = qual.0;
        msg.prio = qual.1;
        msg.send_semantics = qual.2;

        // XXX Why isn't direction set in MessageBody? Then it might
        // not need to be an Option.
        msg.direction = match parser_state.direction.get() {
            Some(d) => d,
            None => {
                parser_state.add_error(&msg.name.loc,
                                       "Missing message direction.");
                Direction::ToParent // Dummy value.
            }
        };

        msg
    },
};

MessageBody: MessageDecl = {
    <name:Identifier> "(" <in_params:ParamList> ")" <out_params:MessageOutParams> <modifiers:MessageModifier*> => {
        let mut decl = MessageDecl::new(name);
        decl.add_in_params(in_params);
        decl.add_out_params(out_params);
        decl.add_modifiers(modifiers);
        decl
    },
};

MessageOutParams: Vec<Param> = {
    "returns" "(" <l:ParamList> ")" => l,
    => Vec::new(),
};

MessageModifier: MessageModifier = {
    "verify" => MessageModifier::Verify,
    "compress" => MessageModifier::Compress(Compress::Enabled),
    "compressall" => MessageModifier::Compress(Compress::All),
};


//--------------------
// Minor stuff

Nesting: Nesting = {
    "not" => Nesting::None,
    "inside_sync" => Nesting::InsideSync,
    "inside_cpow" => Nesting::InsideCpow,
};

Priority: Priority = {
    "normal" => Priority::Normal,
    "high" => Priority::High,
    "input" => Priority::Input,
};

SendQualifier: (Option<Nesting>, Option<Priority>) = {
    "nested" "(" <n:Nesting> ")" => (Some(n), None),
    "prio" "(" <p:Priority> ")" => (None, Some(p)),
};

SendQualifierList: (Option<Nesting>, Option<Priority>) = {
    <q:SendQualifier> <l:SendQualifierList> => (l.0.or(q.0), l.1.or(q.1)),
    => (None, None)
};

SendSemanticsQual: (Nesting, Priority, SendSemantics) = {
    <l:SendQualifierList> "async" =>
        (l.0.unwrap_or(Nesting::None),
         l.1.unwrap_or(Priority::Normal),
         SendSemantics::Async),
    <l:SendQualifierList> "sync" =>
        (l.0.unwrap_or(Nesting::None),
         l.1.unwrap_or(Priority::Normal),
         SendSemantics::Sync),
    "intr" => (Nesting::None, Priority::Normal, SendSemantics::Intr)
};

ProtocolSendSemanticsQual: (Nesting, SendSemantics) = {
    "async" => (Nesting::None, SendSemantics::Async),
    "sync" => (Nesting::None, SendSemantics::Sync),
    "nested" "(" "upto" <n:Nesting> ")" "async" => (n, SendSemantics::Async),
    "nested" "(" "upto" <n:Nesting> ")" "sync" => (n, SendSemantics::Sync),
    "intr" => (Nesting::None, SendSemantics::Intr),
};

ParamList: Vec<Param> = {
    <l:ParamList> "," <p:Param> => {
        let mut l = l;
        l.push(p);
        l
    },
    <p:Param> => vec![p],
    => Vec::new()
};

Param: Param = {
    <t:Type> <name:Identifier> => Param::new(t, name)
};

Type: TypeSpec = {
    // only actor types are nullable; we check this in the type checker
    <is_nullable: "nullable"?> <t:BasicType> => t.set_nullable(is_nullable.is_some())
};

// ID == CxxType; we forbid qnames here,
// in favor of the |using| declaration
BasicType: TypeSpec = {
    <id:CxxID> <is_array: ("[" "]")?> => {
        TypeSpec::new(QualifiedId::new(id)).set_array(is_array.is_some())
    },
};


//--------------------
// C++ stuff

CxxType: TypeSpec = {
    <id:QualifiedID> => TypeSpec::new(id),
    <id:CxxID> => TypeSpec::new(QualifiedId::new(id)),
};

QualifiedID: QualifiedId = {
    <qual_id:QualifiedID> "::" <id:CxxID> => qual_id.qualify(id),
    <id1:CxxID> "::" <id2:CxxID> => QualifiedId::new(id1).qualify(id2),
};

CxxID: Identifier = {
    Identifier,
    CxxTemplateInst,
};

CxxTemplateInst: Identifier = {
    <t_name:Identifier> "<" <arg:Identifier> ">" => {
        Identifier::new(t_name.id + "<" + &arg.id + ">", t_name.loc)
    }
};
